/*
 * This file is part of the DITA Open Toolkit project.
 *
 * Copyright 2015 Jarno Elovirta
 *
 * See the accompanying LICENSE file for applicable license.
 */
import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.dita-ot'
archivesBaseName = 'dost'
version = '3.7.0-SNAPSHOT'

description = """DITA Open Toolkit"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/"
    }
}
dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'xerces', name: 'xercesImpl', version:'2.12.1'
    implementation group: 'xml-apis', name: 'xml-apis', version:'1.4.01'
    implementation group: 'xml-resolver', name: 'xml-resolver', version:'1.2'
    implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: '10.6'
    implementation group: 'com.ibm.icu', name: 'icu4j', version:'70.1'
    implementation group: 'org.apache.ant', name: 'ant', version:'1.10.12'
    implementation group: 'org.apache.ant', name: 'ant-launcher', version:'1.10.12'
    implementation(group: 'com.google.guava', name: 'guava', version: '25.1-jre') {
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.13.0'
    implementation group: 'org.relaxng', name: 'jing', version: '20181222'
    implementation group: 'org.apache.ant', name: 'ant-apache-resolver', version:'1.10.12'
    testImplementation  group: 'nu.validator.htmlparser', name: 'htmlparser', version:'1.4'
    testImplementation  group: 'junit', name: 'junit', version:'4.13.2'
    testImplementation  group: 'org.xmlunit', name: 'xmlunit-core', version: '2.6.0'
}

jar {
    archiveFileName = "${project.name}.jar"
    exclude "catalog.xml"
}

processResources {
    filter ReplaceTokens, tokens: [
            'version': project.version,
            'release': "${project.version.split('\\.').toList().subList(0, 2).join('.').toString()}".toString()
    ]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

test {
    exclude '**/IntegrationTest*.class'
    exclude '**/EndToEndTest*.class'
}

test.maxParallelForks = 4

// Integration test

def integrationTestSystemProperties = [
        temp_dir : "${buildDir}/tmp/integrationTest",
        dita_dir : "${projectDir}/src/main",
        basedir : "${projectDir}/src/test"
]
if (System.getProperty("test") != null) {
    integrationTestSystemProperties["test"] = System.getProperty("test")
}
if (System.getProperty("log_level") != null) {
    integrationTestSystemProperties["log_level"] = System.getProperty("log_level")
}

task integrationTest(type: Test, dependsOn: 'buildLocal') {
    minHeapSize = "128m"
    maxHeapSize = "1024m"
    systemProperties = integrationTestSystemProperties
    include '**/IntegrationTest*.class'
}

// End to end test

dependencies {
    testImplementation  project(':htmlhelp')
    testImplementation  project(':fo')
    testImplementation  project(':fop')
}

def e2eTestSystemProperties = [
        temp_dir : "${buildDir}/tmp/e2eTest",
        dita_dir : "${projectDir}/src/main",
        basedir : "${projectDir}/src/test"
]
if (System.getProperty("log_level") != null) {
    integrationTestSystemProperties["log_level"] = System.getProperty("log_level")
}

task e2eTest(type: Test, dependsOn: 'buildLocal') {
    minHeapSize = "128m"
    maxHeapSize = "1024m"
    maxParallelForks = 4
    systemProperties = e2eTestSystemProperties
    include '**/EndToEndTest*.class'
}

// Install

defaultTasks 'buildLocal'

clean {
    delete "src/main/lib/"
    delete "src/main/plugins/org.dita.htmlhelp/lib/"
    delete "src/main/plugins/org.dita.pdf2/lib/"
    delete "src/main/plugins/org.dita.pdf2.fop/lib/"
}

task copyInstall(type: Copy) {
    from (jar)
    from (configurations.runtimeClasspath.allArtifacts.files)
    from (configurations.runtimeClasspath) {
        rename "ant-launcher(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant-launcher.jar"
        rename "ant(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant.jar"
    }
    destinationDir = file("src/main/lib")
}
task buildLocal(type: JavaExec, dependsOn: [copyInstall, ":fo:copyInstall", ":fop:copyInstall", ":htmlhelp:copyInstall", ":html5:compileSass"]) {
    description "Build archives and install all plugins with dependencies"
    mainClass = "org.apache.tools.ant.launch.Launcher"
    classpath = sourceSets.main.runtimeClasspath + files("${projectDir}/src/main", "${projectDir}/src/main/config")
    workingDir file("${projectDir}/src/main")
    args "-f"
    args file("${projectDir}/src/main/integrator.xml")
}

// Distribution

ext.distTempDir = file("${buildDir}/tmp/dist")
def cleanVersion = project.version.split('\\.').toList()[2] == "0" ?
        project.version.split('\\.').toList().subList(0, 2).join('.').toString() :
        project.version
def distVersion = project.hasProperty("commit") && (!project.hasProperty("tag") || tag.empty) ?
                  "${project.version}@${commit.substring(0, 7)}" :
                  "${cleanVersion}"
def distFileName = (project.hasProperty("tag") && tag.empty) ?
                   "develop" :
                   "${cleanVersion}"
def bundled = [
        "eclipsehelp": "https://github.com/dita-ot/org.dita.eclipsehelp/releases/download/3.4/org.dita.eclipsehelp-3.4.0.zip",
        "markdown": "https://github.com/jelovirt/org.lwdita/releases/download/2.4.0/org.lwdita-2.4.0.zip",
        "normalize": "https://github.com/dita-ot/org.dita.normalize/archive/1.0.zip",
        "dita11": "https://github.com/dita-ot/org.dita.specialization.dita11/archive/1.1.1.zip",
        "dita12": "https://github.com/dita-ot/org.oasis-open.dita.v1_2/archive/1.2.1.zip",
        "dita20": "https://github.com/dita-ot/dita/releases/download/2.0.0-20211007/org.oasis-open.dita.v2_0.zip",
        "dita20-techcomm": "https://github.com/dita-ot/dita-techcomm/releases/download/2.0.0-20211007/org.oasis-open.dita.techcomm.v2_0.zip",
        "xdita": "https://github.com/oasis-tcs/dita-lwdita/releases/download/v0.2.2/org.oasis-open.xdita.v0_2_2.zip",
        "index": "https://github.com/dita-ot/org.dita.index/releases/download/1.0.0/org.dita.index-1.0.0.zip",
        "axf": "https://github.com/dita-ot/org.dita.pdf2.axf/releases/download/3.6.1/org.dita.pdf2.axf-3.6.1.zip",
        "xep": "https://github.com/dita-ot/org.dita.pdf2.xep/releases/download/3.6.3/org.dita.pdf2.xep-3.6.3.zip",
]

apply from: 'gradle/dist.gradle'

task cleanDistTemp(type: Delete) {
    delete distTempDir
}

task initDist(dependsOn: [jar, cleanDistTemp]) {
    doLast {
        distTempDir.mkdirs()
        println "distVersion: ${distVersion}"
        println "distFileName: ${distFileName}"
    }
}

task copyDistTemp(type: Copy, dependsOn: initDist) {
    from (jar) {
        into "lib"
    }
    from (configurations.runtimeClasspath.allArtifacts.files) {
        into "lib"
    }
    from (configurations.runtimeClasspath) {
        into "lib"
        rename "ant-launcher(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant-launcher.jar"
        rename "ant(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant.jar"
    }
    from (".") {
        include "LICENSE"
    }
    from ("src/main") {
        include "plugins/*/plugin.xml"
        expand(
                version: project.version
        )
    }
    from ("src/main") {
        exclude ".gradle"
        exclude "plugins/**/build.gradle"
        exclude "plugins/**/settings.gradle"
        exclude "plugins/**/.gradle"
        exclude ".*"
        exclude "local.properties"
        include "*.*"
        include "bin/dita"
        include "bin/dita.bat"
        exclude "docsrc/.editorconfig"
        exclude "docsrc/.github"
        exclude "docsrc/.gradle"
        exclude "docsrc/.travis"
        exclude "docsrc/.travis.yml"
        exclude "docsrc/*.md"
        exclude "docsrc/*.xpr"
        exclude "docsrc/build"
        exclude "docsrc/out"
        exclude "docsrc/platform.ditaval"
        exclude "docsrc/temp"
        include "docsrc/**"
        exclude "plugins/*/plugin.xml"
        include "plugins/org.dita.base/**"
        include "plugins/org.dita.html5/**"
        exclude "plugins/org.dita.html5/build"
        exclude "plugins/org.dita.html5/.sass-cache"
        include "plugins/org.dita.htmlhelp/**"
        include "plugins/org.dita.pdf2.axf/**"
        include "plugins/org.dita.pdf2.fop/**"
        exclude "plugins/org.dita.pdf2.fop/build"
        include "plugins/org.dita.pdf2/**"
        exclude "plugins/org.dita.pdf2/src/**"
        exclude "plugins/org.dita.pdf2/build"
        exclude "plugins/org.dita.pdf2/bin/**"
        include "plugins/org.dita.specialization.dita11/**"
        include "plugins/org.dita.xhtml/**"
        include "plugins/org.oasis-open.dita.v1_2/**"
        include "plugins/org.oasis-open.dita.v1_3/**"
        exclude "temp"
        include "config/**"
        include "resources/catalog.xml"
        include "resources/plugin.rnc"
        include "resources/project.rnc"
        // legacy build scripts
        include "startcmd.*"
        include "bin/ant"
        include "bin/ant.*"
    }
    destinationDir = distTempDir
}
copyDistTemp.outputs.dir distTempDir

task integrateDistTemp(type: JavaExec, dependsOn: [copyDistTemp, ":fo:copyDistTemp", ":fop:copyDistTemp", ":htmlhelp:copyDistTemp"]) {
    mainClass = "org.apache.tools.ant.launch.Launcher"
    classpath = sourceSets.main.runtimeClasspath + files("${distTempDir}", "${projectDir}/src/main/config")
    workingDir distTempDir
    args "-f"
    args file("${distTempDir}/integrator.xml")
}

task integrateDistPlugins() {
    dependsOn bundled.collect{ name, url ->
        return tasks.create("integrateDist${name}", JavaExec) {
            dependsOn(integrateDistTemp)
            mainClass = "org.apache.tools.ant.launch.Launcher"
            classpath = sourceSets.main.runtimeClasspath + files("${distTempDir}", "${projectDir}/src/main/config")
            workingDir distTempDir
            args "-f"
            args file("${distTempDir}/integrator.xml")
            args "install"
            args "-Dplugin.file=${url}"
        }.name
    }
}
integrateDistPlugins.mustRunAfter integrateDistTemp

task generateDocs(type: Exec, dependsOn: integrateDistTemp) {
    workingDir = file("${distTempDir}/docsrc")
    commandLine = Os.isFamily(Os.FAMILY_WINDOWS) ? file("${distTempDir}/docsrc/gradlew.bat") : file("${distTempDir}/docsrc/gradlew")
    args = ["--no-daemon", "dist"]
}
generateDocs.mustRunAfter integrateDistPlugins
generateDocs.onlyIf { skipDocs() }

task cleanGenerateDocs(type: Delete) {
    delete "${distTempDir}/temp"
    delete "${distTempDir}/docsrc/.gradle"
    delete "${distTempDir}/docsrc/temp"
}
cleanGenerateDocs.mustRunAfter generateDocs
cleanGenerateDocs.onlyIf { skipDocs() }

def skipDocs() {
    !(project.hasProperty('skipGenerateDocs') && Boolean.parseBoolean(project.property('skipGenerateDocs')))
}

task generateJavadocs(type: Javadoc) {
    classpath = sourceSets.main.compileClasspath
    source = sourceSets.main.allJava
    include "**/org/dita/dost/*.*"
    include "**/org/dita/dost/exception/DITAOTException.*"
    include "**/org/dita/dost/util/Configuration.*"
    options.addStringOption("public")
    title = "${project.description} ${project.version}"
    destinationDir = file("${distTempDir}/doc/api")
}
generateJavadocs.onlyIf { skipDocs() }

def plugins = [
        "org.dita.html5",
        "org.dita.htmlhelp",
        "org.dita.pdf2",
        "org.dita.pdf2.fop",
        "org.dita.xhtml",
]

task distPlugins() {
    dependsOn plugins.collect{ name ->
        def taskName = "distPlugin${name.split("\\.").collect{token -> return token.capitalize()}.join("")}"
        return tasks.create(taskName, Zip) {
            dependsOn(integrateDistTemp)
            from ("${distTempDir}/plugins/${name}") {
                exclude "build"
                exclude "src"
                exclude "out"
            }
            archiveFileName = "${name}-${project.version}.zip"
        }.name
    }
}
distPlugins.mustRunAfter integrateDistPlugins

task distPluginChecksums() {
    doLast {
        ant.checksum(algorithm: "SHA-256", fileext: ".sha256") {
            fileset(dir: "${buildDir}/distributions") {
                include(name: '*.zip')
            }
        }
    }
}
distPluginChecksums.mustRunAfter distPlugins
generateNotices.mustRunAfter distPlugins

task dist(type: Zip, dependsOn: [jar, integrateDistTemp, integrateDistPlugins, distPlugins, distPluginChecksums, generateNotices, generateDocs, cleanGenerateDocs, generateJavadocs]) {
    into("dita-ot-${distVersion}") {
        from (distTempDir) {
            exclude "bin/dita"
            exclude "bin/dita.bat"
            // legacy build scripts
            exclude "startcmd.*"
            exclude "bin/ant"
            exclude "bin/ant.*"
        }
        from (distTempDir) {
            fileMode = 0755
            include "bin/dita"
            include "bin/dita.bat"
            // legacy build scripts
            include "startcmd.*"
            include "bin/ant"
            include "bin/ant.*"
        }
    }
    archiveFileName = "dita-ot-${distFileName}.zip"
}

test {
    inputs.files (
        'src/test/xsl/common/dita-utilities.xspec',
        'src/test/xsl/common/uri-utils.xspec',
        'src/test/xsl/plugins/org.dita.html5/xsl/functions.xspec',
        'src/test/xsl/plugins/org.dita.html5/xsl/simpletable.xspec',
        'src/test/xsl/plugins/org.dita.pdf2/xsl/fo/simpletable.xspec',
        'src/test/xsl/plugins/org.dita.pdf2/xsl/fo/topic.xspec',
        'src/test/xsl/preprocess/conrefImpl.xspec',
        'src/test/xsl/preprocess/maplinkImpl.xspec'
    )
}

// Repository deployment

def getRepositoryUsername() {
    return project.hasProperty('ossrhUsername') ?
            project.property('ossrhUsername') :
            System.env.containsKey('OSSRH_USERNAME') ?
                    System.env['OSSRH_USERNAME'] :
                    ""
}

def getRepositoryPassword() {
    return project.hasProperty('ossrhPassword') ?
            project.property('ossrhPassword') :
            System.env.containsKey('OSSRH_PASSWORD') ?
                    System.env['OSSRH_PASSWORD'] :
                    ""
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            afterEvaluate {
                pom {
                    name = 'DITA Open Toolkit'
                    description = 'DITA-OT base library'
                    url = 'https://www.dita-ot.org/'
                    scm {
                        connection = 'scm:git:https://github.com/dita-ot/dita-ot.git'
                        developerConnection = 'scm:git:git@github.com:dita-ot/dita-ot.git'
                        url = 'https://github.com/dita-ot/dita-ot/'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'robander'
                            name = 'Robert D Anderson'
                            email = 'gorodki@gmail.com'
                        }
                        developer {
                            id = 'jelovirt'
                            name = 'Jarno Elovirta'
                            email = 'jarno@elovirta.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username getRepositoryUsername()
                password getRepositoryPassword()
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
