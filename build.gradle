/*
 * This file is part of the DITA Open Toolkit project.
 *
 * Copyright 2015 Jarno Elovirta
 *
 * See the accompanying LICENSE file for applicable license.
 */
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'maven'

group = 'org.dita-ot'
archivesBaseName = 'dost'
version = '2.5.4'

description = """DITA Open Toolkit"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     mavenCentral()
}
dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'commons-codec', name: 'commons-codec', version:'1.10'
    compile group: 'xerces', name: 'xercesImpl', version:'2.11.0'
    compile group: 'xml-apis', name: 'xml-apis', version:'1.4.01'
    compile group: 'xml-resolver', name: 'xml-resolver', version:'1.2'
    compile group: 'net.sf.saxon', name: 'Saxon-HE', version: '9.8.0-2'
    compile group: 'com.ibm.icu', name: 'icu4j', version:'57.1'
    compile group: 'org.apache.ant', name: 'ant', version:'1.10.1'
    compile group: 'org.apache.ant', name: 'ant-launcher', version:'1.10.1'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.23'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
    runtime group: 'org.apache.ant', name: 'ant-apache-resolver', version:'1.10.1'
    testCompile group: 'nu.validator.htmlparser', name: 'htmlparser', version:'1.4'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.xmlunit', name: 'xmlunit-core', version: '2.3.0'
}

jar.archiveName = "${project.name}.jar"

processResources {
    filter ReplaceTokens, tokens: [
            'version': project.version,
            'release': "${project.version.split('\\.').toList().subList(0, 2).join('.').toString()}".toString()
    ]
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

test {
    exclude '**/IntegrationTest*.class'
}

// Integration test

def integrationTestSystemProperties = [
        temp_dir : "${buildDir}/tmp/integrationTest",
        dita_dir : "${projectDir}/src/main",
        basedir : "${projectDir}/src/test"
]
if (System.getProperty("test") != null) {
    integrationTestSystemProperties["test"] = System.getProperty("test")
}
if (System.getProperty("log_level") != null) {
    integrationTestSystemProperties["log_level"] = System.getProperty("log_level")
}

task integrationTest(type: Test, dependsOn: 'buildLocal') {
    minHeapSize = "128m"
    maxHeapSize = "1024m"
    systemProperties = integrationTestSystemProperties
    include '**/IntegrationTest*.class'
}

// Install

defaultTasks 'buildLocal'

task copyInstall(type: Copy) {
    from (configurations.runtime.allArtifacts.files)
    from (configurations.runtime) {
        rename "ant-launcher(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant-launcher.jar"
        rename "ant(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant.jar"
    }
    destinationDir = file("src/main/lib")
}
task buildLocal(type: JavaExec, dependsOn: [copyInstall, ":fo:copyInstall", ":axf:copyInstall", ":fop:copyInstall", ":xep:copyInstall", ":eclipsehelp:copyInstall", ":htmlhelp:copyInstall", ":html5:compileSass"]) {
    description "Build archives and install all plugins with dependencies"
    main = "org.apache.tools.ant.launch.Launcher"
    classpath = sourceSets.main.runtimeClasspath + files("${projectDir}/src/main", "${projectDir}/src/main/config")
    workingDir file("${projectDir}/src/main")
    args "-f"
    args file("${projectDir}/src/main/integrator.xml")
}

// Distribution

def distTempDir = file("${buildDir}/tmp/dist")
def distVersion = project.hasProperty("commit") && (!project.hasProperty("tag") || tag.empty) ?
                  "${project.version}+${commit.substring(0, 7)}" :
                  "${project.version}"
def distFileName = (project.hasProperty("tag") && tag.empty) ?
                   "develop" :
                   "${project.version}"
def bundled = [
        "markdown": "https://github.com/jelovirt/dita-ot-markdown/releases/download/1.3.0/com.elovirta.dita.markdown_1.3.0.zip",
        "normalize": "https://github.com/dita-ot/org.dita.normalize/archive/1.0.zip",
        "troff": "https://github.com/dita-ot/org.dita.troff/archive/2.5.zip",
        "tocjs": "https://github.com/dita-ot/com.sophos.tocjs/archive/2.5.zip"
]

task cleanDistTemp(type: Delete) {
    delete distTempDir
}

task initDist(dependsOn: [jar, cleanDistTemp]) {
    doLast {
        distTempDir.mkdirs()
        println "distVersion: ${distVersion}"
        println "distFileName: ${distFileName}"
    }
}

task copyDistTemp(type: Copy, dependsOn: initDist) {
    from (configurations.runtime.allArtifacts.files) {
        into "lib"
    }
    from (configurations.runtime) {
        into "lib"
        rename "ant-launcher(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant-launcher.jar"
        rename "ant(-\\d+(\\.\\d+(\\.\\d+)?)?)\\.jar", "ant.jar"
    }
    from (".") {
        include "LICENSE"
    }
    from ("src/main") {
        exclude "**/.gradle"
        exclude "**/build.gradle"
        exclude "**/settings.gradle"
        exclude ".*"
        exclude "local.properties"
        exclude "build.log"
        exclude "build_demo.xml"
        exclude "catalog-dita.txt"
        include "*.*"
        include "bin/dita"
        include "bin/dita.bat"
        exclude "docsrc/*.md"
        exclude "docsrc/*.xpr"
        exclude "docsrc/site.*"
        exclude "docsrc/build"
        exclude "docsrc/temp"
        include "docsrc/**"
        include "dtd/**"
        //include "lib/**/*.properties"
        exclude "lib/configuration.properties"
        exclude "plugins/org.dita-ot.html/**"
        exclude "plugins/org.dita.html5/.sass-cache"
        exclude "plugins/org.dita.html5/build"
        exclude "plugins/org.dita.pdf2/src/**"
        exclude "plugins/org.dita.pdf2/build"
        exclude "plugins/org.dita.pdf2/bin/**"
        exclude "plugins/org.dita.pdf2.axf/build"
        exclude "plugins/org.dita.pdf2.axf/src"
        exclude "plugins/org.dita.pdf2.fop/build"
        exclude "plugins/org.dita.pdf2.xep/build"
        exclude "plugins/org.dita.pdf2.xep/lib/xep/**"
        exclude "plugins/org.dita.pdf2.xep/src"
        exclude "temp"
        include "plugins/**"
        include "config/**"
        exclude "resources/**"
        include "xsl/**"
        // legacy build scripts
        include "startcmd.*"
        include "bin/ant"
        include "bin/ant.*"
    }
    destinationDir = distTempDir
}
copyDistTemp.outputs.dir distTempDir

task updateConfiguration() {
    doLast {
        if (project.hasProperty('commit') && !commit.empty) {
            ant.propertyfile(file: "${distTempDir}/lib/configuration.properties") {
                entry(key: "otversion", operation: "=", value: "${distVersion}")
            }
        }
    }
}
updateConfiguration.mustRunAfter copyDistTemp

task integrateDistTemp(type: JavaExec, dependsOn: [copyDistTemp, ":fo:copyDistTemp", ":axf:copyDistTemp", ":fop:copyDistTemp", ":xep:copyDistTemp", ":eclipsehelp:copyDistTemp", ":htmlhelp:copyDistTemp", updateConfiguration]) {
    main = "org.apache.tools.ant.launch.Launcher"
    classpath = sourceSets.main.runtimeClasspath + files("${distTempDir}", "${projectDir}/src/main/config")
    workingDir distTempDir
    args "-f"
    args file("${distTempDir}/integrator.xml")
}

task integrateDistPlugins() {
    dependsOn bundled.collect{ name, url ->
        return tasks.create("integrateDist${name}", JavaExec) {
            main = "org.apache.tools.ant.launch.Launcher"
            classpath = sourceSets.main.runtimeClasspath + files("${distTempDir}", "${projectDir}/src/main/config")
            workingDir distTempDir
            args "-f"
            args file("${distTempDir}/integrator.xml")
            args "install"
            args "-Dplugin.file=${url}"
        }.name
    }
}
integrateDistPlugins.mustRunAfter integrateDistTemp

task generateDocs(type: JavaExec, dependsOn: integrateDistTemp) {
    main = "org.apache.tools.ant.launch.Launcher"
    classpath = sourceSets.main.runtimeClasspath + files("${distTempDir}", "${projectDir}/src/main/config")
    workingDir distTempDir
    args "-f"
    args file("${distTempDir}/docsrc/build.xml")
    args "-Dbasedir=" + file("${distTempDir}/docsrc")
    args "-Ddita.home=" + distTempDir
    args "dist"
}
generateDocs.mustRunAfter integrateDistPlugins
generateDocs.onlyIf { skipDocs() }

task cleanGenerateDocs(type: Delete) {
    delete "${distTempDir}/docsrc/temp"
}
cleanGenerateDocs.mustRunAfter generateDocs
cleanGenerateDocs.onlyIf { skipDocs() }

def skipDocs() {
    !(project.hasProperty('skipGenerateDocs') && Boolean.parseBoolean(project.property('skipGenerateDocs')))
}

task generateJavadocs(type: Javadoc) {
    classpath = sourceSets.main.compileClasspath
    source = sourceSets.main.allJava
    include "**/org/dita/dost/*.*"
    include "**/org/dita/dost/exception/DITAOTException.*"
    include "**/org/dita/dost/util/Configuration.*"
    options.addStringOption("public")
    title = "${project.description} ${project.version}"
    destinationDir = file("${distTempDir}/doc/api")
}
generateJavadocs.onlyIf { skipDocs() }

task dist(type: Zip, dependsOn: [jar, integrateDistTemp, integrateDistPlugins, generateDocs, cleanGenerateDocs, generateJavadocs]) {
    into("dita-ot-${distVersion}") {
        from (distTempDir) {
            exclude "bin/dita"
            exclude "bin/dita.bat"
            // legacy build scripts
            exclude "startcmd.*"
            exclude "bin/ant"
            exclude "bin/ant.*"
        }
        from (distTempDir) {
            fileMode = 0755
            include "bin/dita"
            include "bin/dita.bat"
            // legacy build scripts
            include "startcmd.*"
            include "bin/ant"
            include "bin/ant.*"
        }
    }
    archiveName "dita-ot-${distFileName}.zip"
}

test {
    inputs.files (
        'src/test/xsl/common/dita-utilities.xspec',
        'src/test/xsl/common/uri-utils.xspec',
        'src/test/xsl/plugins/org.dita.html5/xsl/functions.xspec',
        'src/test/xsl/preprocess/maplinkImpl.xspec'
    )
}
